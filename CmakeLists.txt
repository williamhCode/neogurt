cmake_minimum_required(VERSION 3.5...3.28)
# set before project
set(CMAKE_OSX_DEPLOYMENT_TARGET "11.0" CACHE STRING "Minimum OS X deployment version")

# Project Setup ------------------------------------
project(
  Neogurt
  VERSION 0.2.1
  DESCRIPTION "A Neovim GUI Smooth like Yogurt!"
  LANGUAGES C CXX
)

if(DEV_BUILD)
  execute_process(
    COMMAND git rev-parse --short HEAD
    WORKING_DIRECTORY "${PROJECT_SOURCE_DIR}"
    OUTPUT_VARIABLE GIT_COMMIT_HASH
    OUTPUT_STRIP_TRAILING_WHITESPACE
  )

  set(PROJECT_VERSION_SUFFIX "dev")
  set(PROJECT_VERSION_COMMIT "${GIT_COMMIT_HASH}")
  set(PROJECT_VERSION "${PROJECT_VERSION}-${PROJECT_VERSION_SUFFIX}-${PROJECT_VERSION_COMMIT}")
endif()

# fix https://gitlab.kitware.com/cmake/cmake/-/issues/24104
enable_language(OBJC)

set(CMAKE_CXX_STANDARD 23)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_EXPORT_COMPILE_COMMANDS ON)  # clangd

# Dependencies  -------------------------------------------------
# set(Boost_USE_STATIC_LIBS ON)
find_package(Boost 1.86.0 REQUIRED COMPONENTS
  locale
  process
  program_options
)

# SDL, glm, stb, webgpu, slang
set(SDL_SHARED OFF)
set(SDL_STATIC ON)
add_subdirectory(deps/webgpu_tools EXCLUDE_FROM_ALL)

# msgpack
set(MSGPACK_USE_STATIC_BOOST ON)
add_subdirectory(deps/msgpack EXCLUDE_FROM_ALL)

# freetype
set(FT_REQUIRE_PNG ON)
set(FT_REQUIRE_HARFBUZZ ON)
find_package(Freetype REQUIRED)

# harfbuzz
find_package(PkgConfig REQUIRED)
pkg_check_modules(HARFBUZZ REQUIRED harfbuzz)
add_library(HarfBuzz INTERFACE)
target_include_directories(HarfBuzz INTERFACE ${HARFBUZZ_INCLUDE_DIRS})
target_link_libraries(HarfBuzz INTERFACE ${HARFBUZZ_LINK_LIBRARIES})

# set(UTFCPP_INCLUDE_DIR ${PROJECT_SOURCE_DIR}/deps/utfcpp/source)

# temp fix for https://github.com/blend2d/blend2d/issues/216
set(BLEND2D_NO_JIT OFF)
set(BLEND2D_STATIC ON)
add_subdirectory(deps/blend2d EXCLUDE_FROM_ALL)

# neogurt core config --------------------------------------------
set(NEOGURT_SRC
  app/sdl_window.cpp
  app/sdl_event.cpp
  app/input.cpp
  app/ime.cpp
  app/size.cpp
  app/window_funcs.mm
  app/options.cpp
  app/path.cpp
  app/task_helper.cpp

  editor/cursor.cpp
  editor/grid.cpp
  editor/window.cpp
  editor/highlight.cpp
  editor/font.cpp

  event/manager.cpp
  event/ui_parse.cpp
  event/ui_process.cpp
  event/neogurt_cmd.cpp

  gfx/context.cpp
  gfx/pipeline.cpp
  gfx/renderer.cpp
  gfx/camera.cpp
  gfx/render_texture.cpp
  
  gfx/font_rendering/font.cpp
  gfx/font_rendering/font_locator.mm
  gfx/font_rendering/shape_drawing.cpp
  gfx/font_rendering/shape_drawing.hpp
  gfx/font_rendering/shape_pen.cpp
  gfx/font_rendering/texture_atlas.cpp

  nvim/nvim.cpp
  nvim/msgpack_rpc/client.cpp

  session/manager.cpp

  utils/unicode.cpp
  utils/clock.cpp
  utils/logger.cpp
  utils/timer.cpp
  utils/color.cpp
)
list(TRANSFORM NEOGURT_SRC PREPEND "src/")
add_library(neogurt_core STATIC ${NEOGURT_SRC})

target_compile_definitions(neogurt_core PUBLIC
  VERSION="${PROJECT_VERSION}"
  VERSION_MAJOR="${PROJECT_VERSION_MAJOR}"
  VERSION_MINOR="${PROJECT_VERSION_MINOR}"
  VERSION_PATCH="${PROJECT_VERSION_PATCH}"
  VERSION_SUFFIX="${PROJECT_VERSION_SUFFIX}"
  VERSION_COMMIT="${PROJECT_VERSION_COMMIT}"
  
  ROOT_DIR="${PROJECT_SOURCE_DIR}"
)

target_include_directories(neogurt_core PUBLIC 
  "${PROJECT_SOURCE_DIR}/src"
)

# custom LLVM path in github actions, else use brew
if(NOT DEFINED ENV{LLVM_PATH})
  execute_process(
    COMMAND brew --prefix llvm
    OUTPUT_VARIABLE LLVM_PATH
    OUTPUT_STRIP_TRAILING_WHITESPACE
  )
  set(ENV{LLVM_PATH} "${LLVM_PATH}")
endif()

# https://github.com/Homebrew/homebrew-core/issues/67427
execute_process(
  COMMAND brew --prefix icu4c
  OUTPUT_VARIABLE ICU_PATH
  OUTPUT_STRIP_TRAILING_WHITESPACE
)
target_link_directories(neogurt_core PUBLIC
  "${ICU_PATH}/lib"
)
target_link_libraries(neogurt_core PUBLIC 
  Boost::locale
  Boost::process
  Boost::program_options
  webgpu_tools
  msgpack-cxx
  Freetype::Freetype
  HarfBuzz
  blend2d::blend2d

  # https://github.com/llvm/llvm-project/issues/77653
  # stack unwinding breaking and linking breaks without this
  "-L$ENV{LLVM_PATH}/lib/c++" 
  "-L$ENV{LLVM_PATH}/lib/unwind"
  "-lunwind"
)

target_compile_options(neogurt_core PUBLIC
  -Wall -Wextra -Wshadow -pedantic 
  -Wno-missing-field-initializers
  -Wno-deprecated-declarations
  -Wno-gnu-anonymous-struct
  -Wno-nested-anon-types
  # jthread clang
  -fexperimental-library
  -Wno-error=narrowing
  "--embed-dir=${PROJECT_SOURCE_DIR}/res"
)

if(XCODE)
  target_compile_definitions(neogurt_core PUBLIC
    XCODE=1
  )
  set_target_properties(neogurt_core PROPERTIES
    XCODE_GENERATE_SCHEME ON
    XCODE_SCHEME_ENABLE_GPU_FRAME_CAPTURE_MODE "Metal"
    XCODE_SCHEME_ENVIRONMENT "PATH=$ENV{PATH};DAWN_DEBUG_BREAK_ON_ERROR=1"
    XCODE_SCHEME_ADDRESS_SANITIZER ON
    XCODE_SCHEME_UNDEFINED_BEHAVIOUR_SANITIZER ON
  )
  set(CMAKE_XCODE_ATTRIBUTE_OTHER_CODE_SIGN_FLAGS "-o linker-signed")
  set(CMAKE_XCODE_ATTRIBUTE_COMPILER_INDEX_STORE_ENABLE "No")
endif()

# neogurt app -----------------------------------------------
add_executable(neogurt src/main.cpp)
target_link_libraries(neogurt PRIVATE neogurt_core)

if(CMAKE_BUILD_TYPE MATCHES RelWithDebInfo)
  set(RESOURCE_DIR "${PROJECT_SOURCE_DIR}/res")
  file(GLOB_RECURSE RESOURCE_FILES
    "${RESOURCE_DIR}/shaders/*"
    "${RESOURCE_DIR}/lua/*"
  )
  target_sources(neogurt PRIVATE "${RESOURCE_FILES}")

  if(APPLE)
    # Create .app bundle --------------------------------------------
    # ensure relative paths of files in res/ are maintained in app bundle
    # NOTE: rerun build-setup when adding resource files
    foreach (FILE ${RESOURCE_FILES})
      file(RELATIVE_PATH FILE_REL_PATH "${RESOURCE_DIR}" "${FILE}")
      get_filename_component(FILE_REL_DIR "${FILE_REL_PATH}" DIRECTORY)
      set_source_files_properties("${FILE}" PROPERTIES
        MACOSX_PACKAGE_LOCATION "Resources/${FILE_REL_DIR}"
      )
    endforeach()

    set_target_properties(neogurt PROPERTIES
      MACOSX_BUNDLE TRUE
      MACOSX_BUNDLE_INFO_PLIST "${RESOURCE_DIR}/Info.plist.in"
    )

    # Fixup Bundle -------------------------------------------------
    # install the macos bundle (.app) to CMAKE_INSTALL_PREFIX
    install(TARGETS neogurt
      BUNDLE DESTINATION .
    )

    set(APP "\${CMAKE_INSTALL_PREFIX}/Neogurt.app")
    # dynamic lib paths for fixup_bundle (for those not in rpath)
    set(DIRS
      "${CMAKE_BINARY_DIR}/_deps/dawn-src/lib"  # libwebgpu_dawn.dylib
      "${CMAKE_BINARY_DIR}/_deps/slang-src/lib"  # libslang.dylib
      "$ENV{LLVM_PATH}/lib/c++"
      "$ENV{LLVM_PATH}/lib/unwind"
    )

    install(CODE "
      include(BundleUtilities)
      fixup_bundle(\"${APP}\" \"\" \"${DIRS}\")
      execute_process(COMMAND codesign --force --deep --sign - \"${APP}\")

      # message(\"Generating dSYM for Neogurt...\")
      # execute_process(COMMAND dsymutil \"${APP}/Contents/MacOS/neogurt\" 
      #                 OUTPUT_VARIABLE DSYM_OUTPUT
      #                 ERROR_VARIABLE DSYM_ERROR
      #                 RESULT_VARIABLE DSYM_RESULT)

      # if(DSYM_RESULT)
      #   message(WARNING \"dsymutil failed: \${DSYM_ERROR}\")
      # else()
      #   file(INSTALL DESTINATION \"\${CMAKE_INSTALL_PREFIX}\"
      #       TYPE DIRECTORY FILES \"${CMAKE_BINARY_DIR}/neogurt.dSYM\")
      #   message(\"dSYM bundle copied to \${CMAKE_INSTALL_PREFIX}/neogurt.dSYM\")
      # endif()

    ")

    # Package into .dmg ------------------------------------------
    set(CPACK_GENERATOR DragNDrop)
    set(CPACK_PACKAGE_FILE_NAME "Neogurt-${PROJECT_VERSION}-${CMAKE_HOST_SYSTEM_PROCESSOR}")
    include(CPack)
  endif()
endif()

# tests ----------------------------
add_executable(harfbuzz_test test/harfbuzz_test.cpp)
target_link_libraries(harfbuzz_test PRIVATE neogurt_core)

add_executable(freetype_test test/freetype_test.cpp)
target_link_libraries(freetype_test PRIVATE neogurt_core)

add_executable(blend2d_test test/blend2d_test.cpp)
target_link_libraries(blend2d_test PRIVATE neogurt_core)
