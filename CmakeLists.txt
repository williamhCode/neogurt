cmake_minimum_required(VERSION 3.5...3.28)

set(CMAKE_OSX_DEPLOYMENT_TARGET "11.0" CACHE STRING "Minimum OS X deployment version")

project(
  Neogurt
  VERSION 0.1.5
  DESCRIPTION "A Neovim GUI Smooth like Yogurt!"
  LANGUAGES C CXX
)

if (DEV_BUILD)
  execute_process(
    COMMAND git rev-parse --short HEAD
    WORKING_DIRECTORY ${PROJECT_SOURCE_DIR}
    OUTPUT_VARIABLE GIT_COMMIT_HASH
    OUTPUT_STRIP_TRAILING_WHITESPACE
  )

  set(PROJECT_VERSION_SUFFIX "dev")
  set(PROJECT_VERSION_COMMIT ${GIT_COMMIT_HASH})
  set(PROJECT_VERSION "${PROJECT_VERSION}-${PROJECT_VERSION_SUFFIX}-${PROJECT_VERSION_COMMIT}")
endif()

# fix https://gitlab.kitware.com/cmake/cmake/-/issues/24104
enable_language(OBJC) 

set(CMAKE_CXX_STANDARD 23)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

set(CMAKE_EXPORT_COMPILE_COMMANDS ON)  # clangd

# deps ---------------------------
set(Boost_USE_STATIC_LIBS ON)
# set(Boost_USE_DEBUG_LIBS OFF)
find_package(Boost 1.86.0 REQUIRED COMPONENTS
  locale
  process
  program_options
)

add_subdirectory(deps/webgpu_tools EXCLUDE_FROM_ALL)

set(MSGPACK_USE_STATIC_BOOST ON)
add_subdirectory(deps/msgpack EXCLUDE_FROM_ALL)

set(FT_REQUIRE_PNG ON)
find_package(Freetype REQUIRED)

set(UTFCPP_INCLUDE_DIR ${PROJECT_SOURCE_DIR}/deps/utfcpp/source)

# temp fix for https://github.com/blend2d/blend2d/issues/216
set(BLEND2D_NO_JIT OFF)
add_subdirectory(deps/blend2d EXCLUDE_FROM_ALL)

# neogurt target -------------------------
set(APP_SRC
	main.cpp

  app/sdl_window.cpp
  app/sdl_event.cpp
  app/input.cpp
  app/ime.cpp
  app/size.cpp
  app/window_funcs.mm
  app/options.cpp
  app/path.cpp
  app/task_helper.cpp

  editor/state.cpp
  editor/cursor.cpp
  editor/grid.cpp
  editor/window.cpp
  editor/highlight.cpp
  editor/font.cpp

  gfx/context.cpp
  gfx/pipeline.cpp
  gfx/renderer.cpp
  gfx/camera.cpp
  gfx/render_texture.cpp
  
  gfx/font_rendering/font.cpp
  gfx/font_rendering/font_locator.mm
  gfx/font_rendering/shape_drawing.cpp
  gfx/font_rendering/shape_drawing.hpp
  gfx/font_rendering/shape_pen.cpp
  gfx/font_rendering/texture_atlas.cpp

  nvim/nvim.cpp
  nvim/event.cpp
  nvim/events/ui.cpp
  nvim/events/user.cpp
  nvim/msgpack_rpc/client.cpp

  session/manager.cpp

  utils/unicode.cpp
  utils/clock.cpp
  utils/logger.cpp
  utils/timer.cpp
  utils/color.cpp
)
list(TRANSFORM APP_SRC PREPEND "src/")

add_executable(neogurt ${APP_SRC})

target_compile_definitions(neogurt PRIVATE
  VERSION="${PROJECT_VERSION}"
  VERSION_MAJOR="${PROJECT_VERSION_MAJOR}"
  VERSION_MINOR="${PROJECT_VERSION_MINOR}"
  VERSION_PATCH="${PROJECT_VERSION_PATCH}"
  VERSION_SUFFIX="${PROJECT_VERSION_SUFFIX}"
  VERSION_COMMIT="${PROJECT_VERSION_COMMIT}"
  
  ROOT_DIR="${PROJECT_SOURCE_DIR}"
)

target_include_directories(neogurt PRIVATE 
  ${PROJECT_SOURCE_DIR}/src
  ${UTFCPP_INCLUDE_DIR}
  ${FREETYPE_INCLUDE_DIRS}
)

# custom LLVM path in github actions, else use brew
if (NOT DEFINED ENV{LLVM_PATH})
  execute_process(COMMAND brew --prefix llvm OUTPUT_VARIABLE LLVM_PATH OUTPUT_STRIP_TRAILING_WHITESPACE)
  set(ENV{LLVM_PATH} ${LLVM_PATH})
endif()

# https://github.com/Homebrew/homebrew-core/issues/67427
execute_process(COMMAND brew --prefix icu4c OUTPUT_VARIABLE ICU_PATH OUTPUT_STRIP_TRAILING_WHITESPACE)
target_link_directories(neogurt PRIVATE
  "${ICU_PATH}/lib"
)

target_link_libraries(neogurt PRIVATE 
  Boost::locale
  Boost::process
  Boost::program_options
  webgpu_tools
  msgpack-cxx
  ${FREETYPE_LIBRARIES}
  blend2d::blend2d
  # https://github.com/llvm/llvm-project/issues/77653
  # stack unwinding breaking and linking breaks without this
  "-L$ENV{LLVM_PATH}/lib/c++ -L$ENV{LLVM_PATH}/lib/unwind -lunwind"
)

target_compile_options(neogurt PRIVATE
  -Wall -Wextra -Wshadow -pedantic 
  # -Wno-missing-field-initializers
  # -Wmissing-designated-field-initializers
  -Wno-deprecated-declarations
  -Wno-gnu-anonymous-struct
  -Wno-nested-anon-types
  # jthread clang
  -fexperimental-library
  -Wno-error=narrowing
  --embed-dir=${PROJECT_SOURCE_DIR}/res
)

if (XCODE)
  target_compile_definitions(neogurt PRIVATE
    XCODE=1
  )
  set_target_properties(neogurt PROPERTIES
    XCODE_GENERATE_SCHEME ON
    XCODE_SCHEME_ENABLE_GPU_FRAME_CAPTURE_MODE "Metal"
    XCODE_SCHEME_ENVIRONMENT "PATH=$ENV{PATH};DAWN_DEBUG_BREAK_ON_ERROR=1"
    XCODE_SCHEME_ADDRESS_SANITIZER ON
    XCODE_SCHEME_UNDEFINED_BEHAVIOUR_SANITIZER ON
  )
  set(CMAKE_XCODE_ATTRIBUTE_OTHER_CODE_SIGN_FLAGS "-o linker-signed")
  set(CMAKE_XCODE_ATTRIBUTE_COMPILER_INDEX_STORE_ENABLE "No")
endif()

# app generation
if (CMAKE_BUILD_TYPE MATCHES RelWithDebInfo)
  set(RESOURCE_DIR ${PROJECT_SOURCE_DIR}/res)
  file(GLOB_RECURSE RESOURCE_FILES
    "${RESOURCE_DIR}/shaders/*"
    "${RESOURCE_DIR}/lua/*"
  )
  target_sources(neogurt PRIVATE
    ${RESOURCE_FILES}
  )

  if (APPLE)
    # NOTE: rerun make build-setup when adding resource files
    foreach (FILE ${RESOURCE_FILES})
      file(RELATIVE_PATH FILE_REL_PATH "${RESOURCE_DIR}" ${FILE})
      get_filename_component(FILE_REL_DIR ${FILE_REL_PATH} DIRECTORY)
      set_source_files_properties(${FILE} PROPERTIES
        MACOSX_PACKAGE_LOCATION "Resources/${FILE_REL_DIR}"
      )
    endforeach()

    set_target_properties(neogurt PROPERTIES
      MACOSX_BUNDLE TRUE
      MACOSX_BUNDLE_INFO_PLIST "${RESOURCE_DIR}/Info.plist.in"
    )

    install(TARGETS neogurt
      BUNDLE DESTINATION . COMPONENT Runtime
    )

    set(APPS "\${CMAKE_INSTALL_PREFIX}/Neogurt.app")
    set(DIRS
      ${CMAKE_BINARY_DIR}/_deps/dawn-src/lib  # libwebgpu_dawn.dylib
      $ENV{LLVM_PATH}/lib/c++
      $ENV{LLVM_PATH}/lib/unwind
    )

    install(CODE "
      include(BundleUtilities)
      fixup_bundle(\"${APPS}\" \"\" \"${DIRS}\")
    ")

    install(CODE "
      execute_process(COMMAND codesign --force --deep --sign - \"${APPS}\")
    ")

    set(CPACK_GENERATOR DragNDrop)
    set(CPACK_PACKAGE_FILE_NAME "Neogurt-${PROJECT_VERSION}-${CMAKE_HOST_SYSTEM_PROCESSOR}")
    include(CPack)
  endif()
endif()
